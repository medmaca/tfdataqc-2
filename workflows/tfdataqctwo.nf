/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT MODULES / SUBWORKFLOWS / FUNCTIONS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
include { FASTQC } from '../modules/nf-core/fastqc/main'
include { MULTIQC } from '../modules/nf-core/multiqc/main'
include { FASTQC_FASTP } from '../subworkflows/local/fastqc_fastp'
include { SORTMERNA } from '../modules/nf-core/sortmerna/main'
include { SORTMERNA as SORTMERNA_INDEX } from '../modules/nf-core/sortmerna/main'
include { KRAKEN2_WF } from '../subworkflows/local/kraken2_wf/main.nf'
include { paramsSummaryMap } from 'plugin/nf-schema'
include { paramsSummaryMultiqc } from '../subworkflows/nf-core/utils_nfcore_pipeline'
include { softwareVersionsToYAML } from '../subworkflows/nf-core/utils_nfcore_pipeline'
include { methodsDescriptionText } from '../subworkflows/local/utils_nfcore_tfdataqctwo_pipeline'

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    RUN MAIN WORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow TFDATAQCTWO {
    take:
    ch_samplesheet // channel: samplesheet read in from --input

    main:

    ch_versions = Channel.empty()
    ch_multiqc_files = Channel.empty()
    //
    // SUBWORKFLOW: Run FASTQC_FASTP
    //
    FASTQC_FASTP(
        ch_samplesheet,
        params.fastp_save_trimmed_fail,
        params.fastp_use_fastplong,
    )

    FASTQC_FASTP.out.reads.set { ch_filtered_reads }


    ch_multiqc_files = ch_multiqc_files.mix(FASTQC_FASTP.out.multiqc_files)
    ch_versions = ch_versions.mix(FASTQC_FASTP.out.version)



    Channel.fromPath("${params.rrna_fastas}/*.{fa,fna,fasta,fa.gz,fasta.gz}", checkIfExists: true)
        .collect()
        .map { ['rrna_refs', it] }
        .set { ch_sortmerna_fastas }

    SORTMERNA_INDEX(
        [[], []],
        ch_sortmerna_fastas,
        [[], []],
    )


    ch_sortmerna_index = SORTMERNA_INDEX.out.index


    SORTMERNA(
        ch_filtered_reads,
        ch_sortmerna_fastas,
        ch_sortmerna_index,
    )





    ch_multiqc_files = ch_multiqc_files.mix(SORTMERNA.out.log.collect { it[1] }.ifEmpty([]))
    ch_versions = ch_versions.mix(SORTMERNA.out.versions.first())


    KRAKEN2_WF(
        params.kraken2_db,
        SORTMERNA.out.reads,
    )
    ch_multiqc_files = ch_multiqc_files.mix(KRAKEN2_WF.out.multiqc_files)
    ch_versions = ch_versions.mix(KRAKEN2_WF.out.version)





    //
    // Collate and save software versions
    //
    softwareVersionsToYAML(ch_versions)
        .collectFile(
            storeDir: "${params.outdir}/pipeline_info",
            name: 'nf_core_' + 'tfdataqctwo_software_' + 'mqc_' + 'versions.yml',
            sort: true,
            newLine: true,
        )
        .set { ch_collated_versions }


    //
    // MODULE: MultiQC
    //
    ch_multiqc_config = Channel.fromPath(
        "${projectDir}/assets/multiqc_config.yml",
        checkIfExists: true
    )
    ch_multiqc_custom_config = params.multiqc_config
        ? Channel.fromPath(params.multiqc_config, checkIfExists: true)
        : Channel.empty()
    ch_multiqc_logo = params.multiqc_logo
        ? Channel.fromPath(params.multiqc_logo, checkIfExists: true)
        : Channel.empty()

    summary_params = paramsSummaryMap(
        workflow,
        parameters_schema: "nextflow_schema.json"
    )
    ch_workflow_summary = Channel.value(paramsSummaryMultiqc(summary_params))
    ch_multiqc_files = ch_multiqc_files.mix(
        ch_workflow_summary.collectFile(name: 'workflow_summary_mqc.yaml')
    )
    ch_multiqc_custom_methods_description = params.multiqc_methods_description
        ? file(params.multiqc_methods_description, checkIfExists: true)
        : file("${projectDir}/assets/methods_description_template.yml", checkIfExists: true)
    ch_methods_description = Channel.value(
        methodsDescriptionText(ch_multiqc_custom_methods_description)
    )

    ch_multiqc_files = ch_multiqc_files.mix(ch_collated_versions)
    ch_multiqc_files = ch_multiqc_files.mix(
        ch_methods_description.collectFile(
            name: 'methods_description_mqc.yaml',
            sort: true,
        )
    )

    MULTIQC(
        ch_multiqc_files.collect(),
        ch_multiqc_config.toList(),
        ch_multiqc_custom_config.toList(),
        ch_multiqc_logo.toList(),
        [],
        [],
    )

    emit:
    multiqc_report = MULTIQC.out.report.toList() // channel: /path/to/multiqc_report.html
    versions = ch_versions // channel: [ path(versions.yml) ]
}
